buildscript {
    repositories {
        jcenter()
        maven { url "https://plugins.gradle.org/m2/" }
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:2.2.3'
        classpath 'com.neenbedankt.gradle.plugins:android-apt:1.8'
        classpath 'gradle.plugin.com.kageiit:lintrules:1.1.2'
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.7.3'
        classpath 'org.kt3k.gradle.plugin:coveralls-gradle-plugin:2.7.1'
        classpath "com.github.ben-manes:gradle-versions-plugin:0.13.0"
    }
}

repositories {
    jcenter()
}

apply plugin: 'checkstyle'
apply plugin: 'com.github.ben-manes.versions'

apply plugin: 'jacoco'
apply plugin: 'com.github.kt3k.coveralls'


checkstyle {
    toolVersion = '6.14.1'
}

task checkstyle(type: Checkstyle) {
    configFile rootProject.file('checkstyle.xml')
    source fileTree({ 'library/src/main/java'; 'compiler/src/main/java' })
    ignoreFailures false
    showViolations true
    include '**/*.java'
    classpath = files()
}

ext {
    jcenterGroup = 'universal-adapter'
    gitHubUrl = 'https://github.com/lliepmah/AndroidUniversalAdapter'

    libraryVersionCode = (System.getenv('TRAVIS_BUILD_NUMBER') ?: 3).toInteger()

    println "libraryVersionCode=$libraryVersionCode"

    majorVersion = 0
    minorVersion = 0
    libraryVersion = "$majorVersion.$minorVersion.$libraryVersionCode"

    minSdkVersion = 15
    targetSdkVersion = 25
    compileSdkVersion = 25

    buildToolsVersion = '25.0.1'
    sourceCompatibilityVersion = JavaVersion.VERSION_1_6
    targetCompatibilityVersion = JavaVersion.VERSION_1_6
}

def androidToolsVersion = '25.1.2'
def supportLibraryVersion = '25.1.0'

ext.deps = [
        /* Android */
        android            : 'com.google.android:android:4.1.1.4',

        supportCompat      : "com.android.support:appcompat-v7:$supportLibraryVersion",
        cardview           : "com.android.support:cardview-v7:$supportLibraryVersion",
        supportAnnotations : "com.android.support:support-annotations:$supportLibraryVersion",
        recyclerView       : "com.android.support:recyclerview-v7:$supportLibraryVersion",

        supportTestRunner  : 'com.android.support.test:runner:0.5',
        androidGradle      : 'com.android.tools.build:gradle:2.2.0',

        butterknife        : "com.jakewharton:butterknife:8.4.0",
        butterknifeCompiler: "com.jakewharton:butterknife-compiler:8.4.0",

        /* Analysers */
        lint               : "com.android.tools.lint:lint:$androidToolsVersion",
        lintApi            : "com.android.tools.lint:lint-api:$androidToolsVersion",
        lintChecks         : "com.android.tools.lint:lint-checks:$androidToolsVersion",
        lintTests          : "com.android.tools.lint:lint-tests:$androidToolsVersion",

        /* Java */
        javapoet           : 'com.squareup:javapoet:1.8.0',
        javaparser         : 'com.github.javaparser:javaparser-core:2.5.1',

        /* Test dependencies */
        junit              : 'junit:junit:4.12',
        truth              : 'com.google.truth:truth:0.30',
        robolectric        : 'org.robolectric:robolectric:3.1.4',
        openglApi          : 'org.khronos:opengl-api:gl1.1-android-2.1_r1',
        compiletesting     : 'com.google.testing.compile:compile-testing:0.10',
        autoservice        : 'com.google.auto.service:auto-service:1.0-rc2',
        autocommon         : 'com.google.auto:auto-common:0.8',
        hamcrest           : 'org.hamcrest:java-hamcrest:2.0.0.0',
        mockito            : 'org.mockito:mockito-all:1.10.19',
        espressoCore       : 'com.android.support.test.espresso:espresso-core:2.2.2',

        powermockMockito   : 'org.powermock:powermock-api-mockito:1.6.6',
        powermockJunit4    : 'org.powermock:powermock-module-junit4:1.6.6',

]

allprojects {
    repositories {
        jcenter()
    }
}

def testableProjectsNames = [':compiler', ':library']

task clean(type: Delete) {
    delete rootProject.buildDir
}

def Set<Project> publishedProjects = subprojects.findAll() {
    testableProjectsNames.contains(it.path)
}

task jacocoRootReport(type: JacocoReport, group: 'Coverage reports') {
    group = 'Reporting'
    description = 'Generates an aggregate report from all subprojects'

    reports {
        html.enabled = true // human readable
        xml.enabled = true // required by coveralls
    }

//    jacocoClasspath = project.configurations['androidJacocoAnt']

    sourceDirectories = files(
            publishedProjects.projectDir.collect {
                fileTree(dir: it.path + "/src/main/java")
            })


    classDirectories = files(
            publishedProjects.projectDir.collect {
                fileTree(
                        dir: it.path + "/intermediates/classes/debug",
                        excludes: [
                                '**/R.class',
                                '**/R$*.class',
                                '**/BuildConfig.*',
                                '**/Manifest*.*',
                                '**/*Test*.*',
                                '**/*Assert*.*',
                                '**/*Exception*.*',
                        ]
                )
            })

    sourceDirectories.forEach {
        println "-> source=${it}"
    }

    classDirectories.forEach {
        println "-> class=${it}"
    }

    executionData = fileTree(dir: project.buildDir, includes: ['**/*.exec', '**/*.ec'])

}

task jacocoTestReport(type: JacocoReport) {


}

coveralls {
    sourceDirs = publishedProjects.projectDir.collect {
        fileTree(dir: it.path + "/src/main/java")
    }

//    jacocoReportPath = "${buildDir}/reports/jacoco/jacocoRootReport/jacocoRootReport.xml"
}

tasks.coveralls {
    dependsOn jacocoRootReport
}
