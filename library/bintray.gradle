apply plugin: 'com.jfrog.bintray'
apply plugin: 'maven-publish'

group = jcenterGroup
version = libraryVersion

task sourcesJar(type: Jar) {
    from files('src/main/java')
    classifier = 'sources'
}

artifacts {
    archives sourcesJar
}

publishing {
    publications {
        mavenAndroid(MavenPublication) {
            pom { packaging 'aar' }
            artifact file('build/outputs/aar/library-release.aar')
            artifact sourcesJar
            pom.withXml {
                Node root = asNode()
                root.appendNode('name', 'AndroidUniversalAdapter')
                root.appendNode('description', 'Simple Universal Adapter for Recycler View')
                root.appendNode('url', gitHubUrl)

                def issues = root.appendNode('issueManagement')
                issues.appendNode('system', 'github')
                issues.appendNode('url', "${gitHubUrl}/issues")

                def scm = root.appendNode('scm')
                scm.appendNode('url', "scm:${gitHubUrl}.git")
                scm.appendNode('connection', "scm:${gitHubUrl}.git")

                def license = root.appendNode('licenses').appendNode('license')
                license.appendNode('name', 'The Apache Software License, Version 2.0')
                license.appendNode('url', 'http://www.apache.org/licenses/LICENSE-2.0.txt')
                license.appendNode('distribution', 'repo')

                def dependencies = root.appendNode('dependencies')
                ['compile', 'provided'].each { scope ->
                    project.configurations[scope].allDependencies.each {
                        def dependency = dependencies.appendNode('dependency')
                        dependency.appendNode('groupId', it.group)
                        dependency.appendNode('artifactId', it.name)
                        dependency.appendNode('version', it.version)
                        dependency.appendNode('scope', scope)
                    }
                }
            }
        }
    }
}

bintray {
    Properties properties = new Properties()
    properties.load(project.rootProject.file('local.properties').newDataInputStream())

    user = System.getenv('bintrayUser') ?: properties.getProperty('bintrayUser')
    key = System.getenv('bintrayKey') ?: properties.getProperty('bintrayKey')

    configurations = ['archives']
    publications = ['mavenAndroid']
    publish = true
    pkg {
        repo = 'universal-adapter'
        name = 'library'
        userOrg = user
        licenses = ['Apache-2.0']
        version {
            name = libraryVersion
            vcsTag = "v$libraryVersion"
        }
    }
}